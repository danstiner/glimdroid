plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id 'com.apollographql.apollo3' version '3.0.0'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.danielstiner.glimdroid"
        minSdk 21
        targetSdk 31
        versionCode 17
        versionName "1.17"

        manifestPlaceholders = [
            'appAuthRedirectScheme': 'com.danielstiner.glimdroid'
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                debugSymbolLevel = 'FULL'
            }
        }

        buildTypes.each {
            it.buildConfigField 'String', 'OAUTH_CLIENT_ID', "\"${findProperty("com.danielstiner.glimdroid.oauth.client_id")}\""
            it.buildConfigField 'String', 'OAUTH_REDIRECT_URL', "\"${findProperty("com.danielstiner.glimdroid.oauth.redirect_url")}\""
            it.buildConfigField 'String', 'GLIMESH_BASE_URL', "\"${findProperty("com.danielstiner.glimdroid.glimesh.base_url")}\""
            it.buildConfigField 'String', 'GLIMESH_WEBSOCKET_URL', "\"${findProperty("com.danielstiner.glimdroid.glimesh.websocket_url")}\""
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

apollo {
    packageName.set("com.danielstiner.glimdroid.apollo")

    customScalarsMapping = [
            'NaiveDateTime' : 'java.time.Instant',
    ]
}

dependencies {
    def apollo_version = '3.0.0'
    def glide_version = '4.12.0'
    def work_version = '2.7.1'

    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "com.apollographql.apollo3:apollo-runtime:$apollo_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation 'com.google.android.material:material:1.5.0-rc01'
    implementation 'net.openid:appauth:0.11.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
    implementation 'ch.threema:webrtc-android:94.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.work:work-testing:$work_version"
}